FUNCTION "LGF_CalcCRC32" : DWord
{ S7_Optimized_Access := 'TRUE' }
AUTHOR : Siemens_Industry_Support
FAMILY : LGF
NAME : LGF_CRC32
   VAR_INPUT 
      initValue { S7_PredefinedAssignment := '16#00'} : DWord;
      mask { S7_PredefinedAssignment := '16#00'} : DWord;
      noOfElements { S7_PredefinedAssignment := '0'} : UInt;
   END_VAR

   VAR_OUTPUT 
      error : Bool;
      status : Word;
   END_VAR

   VAR_IN_OUT 
      "array" : Array[*] of Byte;
   END_VAR

   VAR_TEMP 
      tempCRC : DWord;
      tempLowerLimit : DInt;
      tempUpperLimit : DInt;
      tempIndexArray : DInt;
      tempIndexCRC : DInt;
   END_VAR

   VAR CONSTANT 
      SHIFT_ONE_BIT : UInt := 1;
      SHIFT_THREE_BYTES : UInt := 24;
      ARRAY_FIRST_DIMENSION : UInt := 1;
      CRC_LOOP_LOWER_LIMIT : DInt := 0;
      CRC_LOOP_UPPER_LIMIT : DInt := 7;
      STATUS_NO_ERROR : Word := 16#0000;
      ERR_NO_OF_ELEMENTS : Word := 16#8400;
   END_VAR


BEGIN
	REGION Block info header
	  //===============================================================================
	  // SIEMENS AG / (c)Copyright 2019
	  //-------------------------------------------------------------------------------
	  // Title:            LGF_CRC32
	  // Comment/Function: This function makes a CRC calculation according
	  //                   to the CRC-32 algorithm
	  // Library/Family:   LGF (Library General Functions)
	  // Author:           Siemens Digital Industry Support
	  // Tested with:      CPU 1515F-2 PN FW:V2.6
	  // Engineering:      TIA Portal V15.1 Update 2
	  // Restrictions:     ENO disabled - no error handling, no failure possible
	  // Requirements:     PLC (S7-1200 / S7-1500)
	  //-------------------------------------------------------------------------------
	  //=============================================================================
	END_REGION Block info header
	
	REGION DESCRIPTION
	  (/**/)
	END_REGION DESCRIPTION
	
	REGION Initialization and input data processing
	  // Defining initial value for algorithm
	  #tempCRC := #initValue;
	  // Input array size calculation
	  #tempLowerLimit := LOWER_BOUND(ARR := #array, DIM := #ARRAY_FIRST_DIMENSION);
	  #tempUpperLimit := UPPER_BOUND(ARR := #array, DIM := #ARRAY_FIRST_DIMENSION);
	  
	  // check paramter `noOfELements`, if gretaer than zero check if fits to array size
	  IF #noOfElements > 0 THEN
	    IF #noOfElements > (#tempUpperLimit - #tempLowerLimit + 1) THEN
	      #status := #ERR_NO_OF_ELEMENTS;
	      #error := TRUE;
	      ENO := FALSE;
	      #LGF_CalcCRC32 := #tempCRC;
	      RETURN;
	    ELSE
	      #tempUpperLimit := #tempLowerLimit + #noOfElements - 1;
	    END_IF;
	  END_IF;
	END_REGION
	
	REGION CRC calculation
	  FOR #tempIndexArray := #tempLowerLimit TO #tempUpperLimit DO
	    // Perform division using XOR function for appropriate word in array (with shift byte into MSB of 32bit CRC)
	    #tempCRC := #tempCRC XOR SHL(IN := BYTE_TO_DWORD(#array[#tempIndexArray]), N := #SHIFT_THREE_BYTES);
	    
	    FOR #tempIndexCRC := #CRC_LOOP_LOWER_LIMIT TO #CRC_LOOP_UPPER_LIMIT DO
	      // Check if MSB is set
	      IF #tempCRC.%X31 THEN
	        // Shift left and perform division by mask polynomial using XOR function
	        #tempCRC := SHL(IN := #tempCRC, N := #SHIFT_ONE_BIT) XOR #mask;
	      ELSE
	        // Shift left without division
	        #tempCRC := SHL(IN := #tempCRC, N := #SHIFT_ONE_BIT);
	      END_IF;
	    END_FOR;
	    
	  END_FOR;
	END_REGION
	
	REGION Outputs assignment
	  #LGF_CalcCRC32 := #tempCRC;
	  
	  #status := #STATUS_NO_ERROR;
	  #error := FALSE;
	  ENO := TRUE;
	END_REGION
	
END_FUNCTION